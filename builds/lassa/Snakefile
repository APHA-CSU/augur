rule parse:
	input:
		"../../../flora/data/lassa_{segment}.fasta"
	output:
		seq = "results_{segment}/raw_seqs.fasta",
		meta = "results_{segment}/meta.tsv"
	params:
		fields = "strain accession segment date region country host author reference url"
	shell:
		'augur parse --sequences {input} --output {output.seq} --metadata {output.meta} --fields {params.fields}'


rule filter:
	input:
		seq = "results_{segment}/raw_seqs.fasta",
		meta = "results_{segment}/meta.tsv",
		exclude = "metadata/dropped_strains.txt"
	output:
		"results_{segment}/filtered.fasta"
	params:
		vpc = 30,
		cat = "year month"
	shell:
		"augur filter --sequences {input.seq} --output {output} --metadata {input.meta} --viruses_per_cat {params.vpc} --exclude {input.exclude} --cat {params.cat}"


rule align:
	input:
		seq = "results_{segment}/filtered.fasta",
		ref = "metadata/lassa_{segment}.gb"
	output:
		"results_{segment}/aligned.fasta"
	shell:
		'augur align --sequences {input.seq} --output {output} --reference_sequence {input.ref}'


rule tree:
	input:
		aln = "results_{segment}/aligned.fasta",
	output:
		tree = "results_{segment}/tree_raw.nwk",
	params:
		method = 'iqtree'
	shell:
		'augur tree --alignment {input.aln} --output {output.tree} --method {params.method}'

rule timetree:
	input:
		aln = "results_{segment}/aligned.fasta",
		metadata = "results_{segment}/meta.tsv",
		tree = "results_{segment}/tree_raw.nwk",
	output:
		tree = "results_{segment}/tree.nwk",
		node_data = "results_{segment}/node_data.json"
	params:
		clock_rate = 0.0006,
		root = 'min_dev'
	shell:
		'augur tree --alignment {input.aln} --tree {input.tree} --metadata {input.metadata}'
		' --output {output.tree} --node_data {output.node_data}'
		' --timetree --clock_rate {params.clock_rate} --root {params.root}'

rule traits:
	input:
		tree = "results_{segment}/tree.nwk",
		metadata = "results_{segment}/meta.tsv"
	output:
		"results_{segment}/traits.json",
	params:
		cols = "country"
	shell:
		'augur traits --tree {input.tree} --metadata {input.metadata} --output {output} --columns {params.cols}'

rule translate:
	input:
		tree = "results_{segment}/tree.nwk",
		ref = "metadata/lassa_{segment}.gb",
		node_data = "results_{segment}/node_data.json"
	output:
		"results_{segment}/aa_muts.json"
	shell:
		'augur translate --tree {input.tree} --node_data {input.node_data} --output {output} --reference_sequence {input.ref}'

rule export:
	input:
		tree = "results_{segment}/tree.nwk",
		node_data = "results_{segment}/node_data.json",
		traits = "results_{segment}/traits.json",
		aa_muts = "results_{segment}/aa_muts.json",
	output:
		"auspice/lassa_{segment}_tree.json",
	shell:
		'augur export --tree {input.tree} --node_data {input.node_data} --traits {input.traits} --aa_muts {input.aa_muts} --output {output}'

