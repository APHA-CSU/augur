
rule all:
    input: "auspice/zika_tree.json"

# config variables to be used by rules below
# kind of following https://snakemake.readthedocs.io/en/latest/tutorial/advanced.html#step-2-config-files
# however if defined as a dict then cannot be accessed in shell commands
# the advantage of this is that one can (eventually) import rules from a template
# currently only files are defined here - parameters are defined in their own rules
rule config:
	params:
		input_fasta = "../../../fauna/data/zika.fasta",
		fasta_fields = "strain virus accession date region country division city db segment authors url title journal paper_url",
		dropped_strains = "metadata/dropped_strains.txt",
		reference = "metadata/zika_outgroup.gb",
		geo_info = "../../source-data/geo_lat_long.tsv",
		color_maps = "metadata/colors.tsv",
		auspice_config = "metadata/config.json",

config = rules.config.params # so we can use config.x rather than rules.config.params.x
# end of config definition

rule parse:
	input:
		config.input_fasta
	output:
		seq = "results/raw_seqs.fasta",
		meta = "results/meta.tsv"
	shell:
		'augur parse --sequences {input} --output {output.seq} --metadata {output.meta} --fields {config.fasta_fields}'


rule filter:
	input:
		seq = rules.parse.output.seq,
		meta = rules.parse.output.meta,
		exclude = config.dropped_strains
	output:
		"results/filtered.fasta"
	params:
		vpc = 0,
		cat = "year month",
		min_date = 2012
	shell:
		"augur filter --sequences {input.seq} --output {output} --metadata {input.meta} "
			"--viruses_per_cat {params.vpc} "
			"--exclude {input.exclude} --cat {params.cat} --min_date {params.min_date}"


rule align:
	input:
		seq = rules.filter.output,
		ref = config.reference
	output:
		"results/aligned.fasta"
	shell:
		'augur align --sequences {input.seq} --output {output} '
		 '--reference_sequence {input.ref}  --fill_gaps'


rule tree:
	input:
		aln = rules.align.output
	output:
		tree = "results/tree_raw.nwk",
	shell:
		'augur tree --alignment {input.aln} --output {output.tree}'


rule timetree:
	input:
		aln = rules.align.output,
		metadata = rules.parse.output.meta,
		tree = rules.tree.output.tree,
	output:
		node_data = "results/node_data.json",
		tree = "results/tree.nwk",
	shell:
		'augur treetime --tree {input.tree} --alignment {input.aln} '
		    '--metadata {input.metadata}'
			' --output {output.tree} --node_data {output.node_data}'
			' --timetree --date_confidence --time_marginal --coalescent opt'

rule traits:
	input:
		tree = rules.timetree.output.tree,
		metadata = rules.parse.output.meta
	output:
		"results/traits.json",
	params:
		cols = "region country"
	shell:
		'augur traits --confidence --tree {input.tree} --metadata {input.metadata} --output {output} --columns {params.cols}'

rule translate:
	input:
		tree = rules.timetree.output.tree,
		ref = config.reference,
		node_data = rules.timetree.output.node_data,
	output:
		"results/aa_muts.json"
	shell:
		'augur translate --tree {input.tree} --node_data {input.node_data} --output {output} --reference_sequence {input.ref}'

rule export:
	input:
		tree = rules.timetree.output.tree,
		node_data = rules.timetree.output.node_data,
		metadata = rules.parse.output.meta,
		traits = rules.traits.output,
		aa_muts = rules.translate.output,
		color_defs = config.color_maps,
		auspice_config = config.auspice_config,
		geo_info = config.geo_info
	output:
		tree = "auspice/zika_tree.json",
		meta = "auspice/zika_meta.json",
	shell:
		'augur export --tree {input.tree} --metadata {input.metadata}'
			' --node_data {input.node_data} {input.traits} {input.aa_muts}' # node decorations for the tree JSON
			' --color_maps {input.color_defs} --geo_info {input.geo_info} --auspice_config {input.auspice_config}' #for the meta JSON
			' --tree_output {output.tree} --meta_output {output.meta}'
