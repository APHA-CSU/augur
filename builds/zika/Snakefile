rule all:
    input:
        auspice_tree = "auspice/zika_tree.json",
        auspice_meta = "auspice/zika_meta.json"

rule config:
    params:
        input_fasta = "../../../fauna/data/zika.fasta",
        fasta_fields = "strain virus accession date region country division city db segment authors url title journal paper_url",
        dropped_strains = "config/dropped_strains.txt",
        reference = "config/zika_outgroup.gb",
        colors = "config/colors.tsv",
        auspice_config = "config/auspice_config.json"

config = rules.config.params

rule parse:
    input:
       sequences = config.input_fasta
    output:
       sequences = "results/sequences.fasta",
       metadata = "results/metadata.tsv"
    shell:
        """
        augur parse --sequences {input.sequences} \
            --output-sequences {output.sequences} --output-metadata {output.metadata} \
            --fields {config.fasta_fields}
        """

rule filter:
    input:
        sequences = rules.parse.output.sequences,
        metadata = rules.parse.output.metadata,
        exclude = config.dropped_strains
    output:
        sequences = "results/filtered.fasta"
    params:
        sequences_per_category = 20,
        categories = "country year month",
        min_date = 2012
    shell:
        """
        augur filter --sequences {input.sequences} --metadata {input.metadata} \
            --output {output.sequences} \
            --categories {params.categories} \
            --sequences-per-category {params.sequences_per_category} \
            --exclude {input.exclude}  --min-date {params.min_date}
        """

rule align:
    input:
        sequences = rules.filter.output.sequences,
        ref = config.reference
    output:
        alignment = "results/aligned.fasta"
    shell:
        """
        augur align --sequences {input.sequences} --output {output.alignment} \
            --reference-sequence {input.ref} --fill-gaps
        """

rule tree:
    input:
        alignment = rules.align.output.alignment
    output:
        tree = "results/tree_raw.nwk"
    shell:
        """
        augur tree --alignment {input.alignment} --output {output.tree}
        """

rule refine:
    input:
        tree = rules.tree.output.tree,
        alignment = rules.align.output,
        metadata = rules.parse.output.metadata
    output:
        tree = "results/tree.nwk",
        node_data = "results/node_data.json"
    params:
        n_iqd = 4
    shell:
        """
        augur refine --tree {input.tree} --alignment {input.alignment} \
            --metadata {input.metadata} \
            --output {output.tree} --node-data {output.node_data} \
            --timetree --date-confidence --time-marginal --coalescent opt \
            --n-iqd {params.n_iqd}
        """

rule traits:
    input:
        tree = rules.refine.output.tree,
        metadata = rules.parse.output.metadata
    output:
        node_data = "results/traits.json",
    params:
        columns = "region country"
    shell:
        """
        augur traits --tree {input.tree} --metadata {input.metadata} \
            --output {output.node_data} --confidence --columns {params.columns}
        """

rule translate:
    input:
        tree = rules.refine.output.tree,
        node_data = rules.refine.output.node_data,
        ref = config.reference
    output:
        node_data = "results/aa_muts.json"
    shell:
        """
        augur translate --tree {input.tree} --node-data {input.node_data} \
            --output {output.node_data} --reference-sequence {input.ref}
        """

rule export:
    input:
        tree = rules.refine.output.tree,
        metadata = rules.parse.output.metadata,
        node_data = rules.refine.output.node_data,
        traits = rules.traits.output.node_data,
        aa_muts = rules.translate.output.node_data,
        colors = config.colors,
        auspice_config = config.auspice_config
    output:
        auspice_tree = rules.all.input.auspice_tree,
        auspice_meta = rules.all.input.auspice_meta
    shell:
        """
        augur export --tree {input.tree} --metadata {input.metadata} \
            --node-data {input.node_data} {input.traits} {input.aa_muts} \
            --colors {input.colors} --auspice-config {input.auspice_config} \
            --output-tree {output.auspice_tree} --output-meta {output.auspice_meta}
        """
